#include "fabm_driver.h"

!-----------------------------------------------------------------------
!BOP
!
! !MODULE: fabm_hzg_omexdia_p --- Fortran 2003 version of OMEXDIA+P biogeochemical model
!
! !INTERFACE:
   module fabm_hzg_omexdia_p
!
! !DESCRIPTION:
!
! The OMEXDIA+P+MPB model is based on the OMEXDIA model (see Soetard et al. 1996a)
! and is intended to simulate early diagenesis in the sea sediments. The major
! difference to the original OMEXDIA is an added phosphorus cycle.
! P-cycle is added by kai wirtz
! efficient reaction and limitation terms to facilitate simple numerics added by kai wirtz
! MPB model from Hochard et al EcoMod 2010 added by kai wirtz
!
! !USES:
   use fabm_types

   implicit none

!  default: all is private.
   private
!
! !PUBLIC MEMBER FUNCTIONS:
   public type_hzg_omexdia_p
!
! !PRIVATE DATA MEMBERS:
!   real(rk), parameter :: secs_pr_day = 86400.0_rk
!
! !REVISION HISTORY:!
!  Original author(s): Richard Hofmeister & Kai Wirtz
!
!
!#SP0
!!----------------------------------------------------------------------
!! this code is generated by a parser  (conv_nml_fabm.c by kai wirtz)
!!----------------------------------------------------------------------
 ! HZG model types
  type type_var
   real(rk) :: fdet,sdet,pdet,po4,no3,nh3,oxy,odu,mpbCHL,mpbC,mpbN,eps
   real(rk) :: temp,parz,porosity
  end type
  type type_rhs
   real(rk) :: fdet,sdet,pdet,po4,no3,nh3,oxy,odu,mpbCHL,mpbC,mpbN,eps
  end type
! standard fabm model types
  type,extends(type_base_model),public :: type_hzg_omexdia_p
  type (type_state_variable_id)        :: id_fdet,id_sdet,id_pdet,id_po4,id_no3,id_nh3,id_oxy,id_odu,id_mpbCHL,id_mpbC,id_mpbN,id_eps
  type (type_dependency_id)            :: id_temp
  type (type_dependency_id)            :: id_parz
  type (type_dependency_id)            :: id_porosity
  type (type_diagnostic_variable_id)   :: id_adsP, id_denit, id_PrimProd, id_par, id_Q_N, id_Q_chl
  type (type_conserved_quantity_id)    :: id_totC, id_totN, id_totP
  real(rk) ::  fdet_init, sdet_init, pdet_init, po4_init, no3_init, nh3_init, oxy_init, odu_init, mpbCHL_init, mpbC_init, mpbN_init, eps_init
  real(rk) ::  rFast, rSlow, NCrFdet, NCrSdet, PAds, PAdsODU, NH3Ads, rnit, ksO2nitri, rODUox, ksO2oduox, ksO2oxic, ksNO3denit, kinO2denit, kinNO3anox, kinO2anox
  real(rk) ::  mumax, alpha, gamma, Qmin, Qmax, thetamax, uptmax, KNH4, KNO3, KinNH4, keps, resp, Kresp, graz, kout, kexu, rzoo, PAR0max, k0, Achla, bTemp
  logical  ::  PhotoacclimOn, PhosphorusOn, MPhytoBenOn

  contains
!   Model procedures
  procedure :: initialize
  procedure :: do
 end type type_hzg_omexdia_p
!
! !PRIVATE DATA MEMBERS:
 real(rk), parameter :: secs_pr_day = 86400.0_rk
!EOP
!!--------------------------------------------------------------------

 contains
! !IROUTINE: Initialise the omexdia_p model
!
! !INTERFACE:
  subroutine initialize(self, configunit)
! !DESCRIPTION:
!  Here, the namelists are read and the variables exported
!  by the model are registered with FABM.
!
! !INPUT PARAMETERS:
  class (type_hzg_omexdia_p), intent(inout), target :: self
  integer,                  intent(in)            :: configunit
!
! !LOCAL VARIABLES:
  integer    :: namlst=19
!!------- Initial values of model omexdia_p ------- 
  real(rk)  :: fdet_init    ! fast detritus C
  real(rk)  :: sdet_init    ! slow detritus C
  real(rk)  :: pdet_init    ! detritus-P
  real(rk)  :: po4_init     ! dissolved phosphate
  real(rk)  :: no3_init     ! dissolved nitrate
  real(rk)  :: nh3_init     ! dissolved ammonium
  real(rk)  :: oxy_init     ! dissolved oxygen
  real(rk)  :: odu_init     ! dissolved reduced substances
  real(rk)  :: mpbCHL_init  ! MicroPhytoBenthos chlorophyll
  real(rk)  :: mpbC_init    ! MicroPhytoBenthos carbon
  real(rk)  :: mpbN_init    ! MicroPhytoBenthos nitrogen
  real(rk)  :: eps_init     ! Extracellular Polymeric Substances 
!!------- Parameters from nml-list omexdia_p_par ------- 
  real(rk)  :: rFast        ! decay rate fast decay det.
  real(rk)  :: rSlow        ! decay rate slow decay det.
  real(rk)  :: NCrFdet      ! NC ratio fast decay det.
  real(rk)  :: NCrSdet      ! NC ratio slow decay det.
  real(rk)  :: PAds         ! Adsorption coeff phosphorus
  real(rk)  :: PAdsODU      ! PO4-Fe dissolution threshold in terms of [FeS]/ODU
  real(rk)  :: NH3Ads       ! Adsorption coeff ammonium
  real(rk)  :: rnit         ! Max nitrification rate
  real(rk)  :: ksO2nitri    ! half-sat O2 in nitrification
  real(rk)  :: rODUox       ! Max rate oxidation of ODU
  real(rk)  :: ksO2oduox    ! half-sat O2 in oxidation of ODU
  real(rk)  :: ksO2oxic     ! half-sat O2 in oxic mineralis
  real(rk)  :: ksNO3denit   ! half-sat NO3 in denitrif
  real(rk)  :: kinO2denit   ! half-sat O2 inhib denitrif
  real(rk)  :: kinNO3anox   ! half-sat NO3 inhib anoxic min
  real(rk)  :: kinO2anox    ! half-sat O2 inhib anoxic min
!!------- Parameters from nml-list omexdia_p_mpb ------- 
  real(rk)  :: mumax        ! Maximum growth rate 
  real(rk)  :: alpha        ! ific initial slope of the PI-curve 
  real(rk)  :: gamma        ! Mol O2 produced per mol C fixed by photosynthesis 
  real(rk)  :: Qmin         ! Minimum N/C ratio 
  real(rk)  :: Qmax         ! Maximum N/C ratio 
  real(rk)  :: thetamax     ! Maximum Chla/N ratio 
  real(rk)  :: uptmax       ! Maximum uptake rate per carbon unit for NH4 & NO3 
  real(rk)  :: KNH4         ! Half-saturation conc. for NH4 uptake 
  real(rk)  :: KNO3         ! Half-saturation conc. for NO3 uptake 
  real(rk)  :: KinNH4       ! Half-saturation conc. for NO3 uptake inhibition by ammonium 
  real(rk)  :: keps         ! fraction of primary production exudated as EPS 
  real(rk)  :: resp         ! Respiration rate  
  real(rk)  :: Kresp        ! Half-saturation conc. O2 lim. for resp 
  real(rk)  :: graz         ! Grazing rate 
  real(rk)  :: kout         ! Faeces production coeff. 
  real(rk)  :: kexu         ! Nitrogen exudation coeff. 
  real(rk)  :: rzoo         ! Respiration rate for zoobenthos 
  real(rk)  :: PAR0max      ! maximum light intensity 
  real(rk)  :: k0           ! Extinction coefficient of sediment 
  real(rk)  :: Achla        ! Absorption factor of chlorophyll
  real(rk)  :: bTemp        ! Temperature increase        
!!------- Switches for configuring model structure -------
  logical   :: PhotoacclimOn  ! use Photoacclimation
  logical   :: PhosphorusOn  ! resolve Phosphorus cycle
  logical   :: MPhytoBenOn  ! use MicroPhytoBenthos (MPB)

  namelist /omexdia_p_switch/ &
     PhotoacclimOn, PhosphorusOn, MPhytoBenOn

  namelist /omexdia_p_init/ &
     fdet_init, sdet_init, pdet_init, po4_init, no3_init, nh3_init, oxy_init, &
     odu_init, mpbCHL_init, mpbC_init, mpbN_init, eps_init

  namelist /omexdia_p_par/ &
     rFast, rSlow, NCrFdet, NCrSdet, PAds, PAdsODU, NH3Ads, rnit, ksO2nitri, rODUox, &
     ksO2oduox, ksO2oxic, ksNO3denit, kinO2denit, kinNO3anox, kinO2anox

  namelist /omexdia_p_mpb/ &
     mumax, alpha, gamma, Qmin, Qmax, thetamax, uptmax, KNH4, KNO3, KinNH4, keps, resp, &
     Kresp, graz, kout, kexu, rzoo, PAR0max, k0, Achla, bTemp

  fdet_init    = 1.e5_rk            ! mmolC/m**3
  sdet_init    = 1.e6_rk            ! mmolC/m**3
  pdet_init    = 2.e2_rk            ! mmolP/m**3
  po4_init     = 20._rk             ! mmolP/m**3
  no3_init     = 30_rk              ! mmolN/m**3
  nh3_init     = 100._rk            ! mmolN/m**3
  oxy_init     = 10._rk             ! mmolO2/m**3
  odu_init     = 100._rk            ! mmolO2/m**3
  mpbCHL_init  = 0.1_rk             ! mmolN/m**3
  mpbC_init    = 1._rk              ! mmolC/m**3
  mpbN_init    = 0.1_rk             ! mmolN/m**3
  eps_init     = 0.0_rk             ! mmolC/m**3
  rFast        = 0.01_rk            ! 1/day
  rSlow        = 0.00001_rk         ! 1/day
  NCrFdet      = 0.2_rk             ! molN/molC
  NCrSdet      = 0.04_rk            ! molN/molC
  PAds         = 0.3_rk             ! 
  PAdsODU      = 12._rk             ! 
  NH3Ads       = 0.0_rk             ! 
  rnit         = 20._rk             ! 1/d
  ksO2nitri    = 20._rk             ! umolO2/m3
  rODUox       = 20._rk             ! /d
  ksO2oduox    = 1._rk              ! mmolO2/m3
  ksO2oxic     = 3._rk              ! mmolO2/m3
  ksNO3denit   = 30._rk             ! mmolNO3/m3
  kinO2denit   = 1._rk              ! mmolO2/m3
  kinNO3anox   = 1._rk              ! mmolNO3/m3
  kinO2anox    = 1._rk              ! mmolO2/m3
  mumax        = 0.7_rk             ! 1/d
  alpha        = 1.5E-5_rk          ! m2molC/gChl.J
  gamma        = 1._rk              ! mol O2 (mol C)âˆ’1
  Qmin         = 0.05_rk            ! molN/molC
  Qmax         = 0.2_rk             ! molN/molC
  thetamax     = 3.8_rk             ! gChla/molN
  uptmax       = 0.2_rk             ! molN/molC.d
  KNH4         = 3._rk              ! mmolN/m3
  KNO3         = 3._rk              ! mmolN/m3
  KinNH4       = 10._rk             ! mmolN/m3
  keps         = 0.2_rk             ! 
  resp         = 0.1_rk             ! 1/d
  Kresp        = 1._rk              ! mmolO2/m3
  graz         = 0.1_rk             ! 1/d
  kout         = 0.1_rk             ! 
  kexu         = 0.15_rk            ! 
  rzoo         = 0.1_rk             ! 1/d
  PAR0max      = 240_rk             ! I/s
  k0           = 20._rk             ! C/m
  Achla        = 0.02_rk            ! m2/mgChla
  bTemp        = 0.0633_rk          ! 1/oC


!--------- read namelists --------- 
write(0,*) ' read namelists ....'
  open(namlst,file='./omexdia_p_switch.nml',status='old')
  read(namlst,nml=omexdia_p_switch,err=90,end=99)
  open(namlst,file='./omexdia_p_init.nml',status='old')
  read(namlst,nml=omexdia_p_init,err=91,end=100)
  open(namlst,file='./omexdia_p_par.nml',status='old')
  read(namlst,nml=omexdia_p_par,err=92,end=101)
  open(namlst,file='./omexdia_p_mpb.nml',status='old')
  read(namlst,nml=omexdia_p_mpb,err=93,end=102)
  ! Store parameter values in our own derived type
  ! NB: all rates must be provided in values per day,
  ! and are converted here to values per second.

!!------- logical parameters: switches  -------
  call self%get_parameter(self%PhotoacclimOn,  'PhotoacclimOn',  default=PhotoacclimOn)
  call self%get_parameter(self%PhosphorusOn,  'PhosphorusOn',  default=PhosphorusOn)
  call self%get_parameter(self%MPhytoBenOn,  'MPhytoBenOn',  default=MPhytoBenOn)

!!------- model parameters from nml-list omexdia_p_init ------- 
  call self%get_parameter(self%fdet_init    ,'fdet_init',     default=fdet_init)
  call self%get_parameter(self%sdet_init    ,'sdet_init',     default=sdet_init)
  call self%get_parameter(self%no3_init     ,'no3_init',      default=no3_init)
  call self%get_parameter(self%nh3_init     ,'nh3_init',      default=nh3_init)
  call self%get_parameter(self%oxy_init     ,'oxy_init',      default=oxy_init)
  call self%get_parameter(self%odu_init     ,'odu_init',      default=odu_init)
if (PhosphorusOn) then
      call self%get_parameter(self%pdet_init    ,'pdet_init',     default=pdet_init)
      call self%get_parameter(self%po4_init     ,'po4_init',      default=po4_init)
end if
if (MPhytoBenOn) then
      call self%get_parameter(self%mpbCHL_init  ,'mpbCHL_init',   default=mpbCHL_init)
      call self%get_parameter(self%mpbC_init    ,'mpbC_init',     default=mpbC_init)
      call self%get_parameter(self%mpbN_init    ,'mpbN_init',     default=mpbN_init)
      call self%get_parameter(self%eps_init     ,'eps_init',      default=eps_init)
end if

!!------- model parameters from nml-list omexdia_p_par ------- 
  call self%get_parameter(self%rFast        ,'rFast',         default=rFast)
  call self%get_parameter(self%rSlow        ,'rSlow',         default=rSlow)
  call self%get_parameter(self%NCrFdet      ,'NCrFdet',       default=NCrFdet)
  call self%get_parameter(self%NCrSdet      ,'NCrSdet',       default=NCrSdet)
  call self%get_parameter(self%PAds         ,'PAds',          default=PAds)
  call self%get_parameter(self%PAdsODU      ,'PAdsODU',       default=PAdsODU)
  call self%get_parameter(self%NH3Ads       ,'NH3Ads',        default=NH3Ads)
  call self%get_parameter(self%rnit         ,'rnit',          default=rnit)
  call self%get_parameter(self%ksO2nitri    ,'ksO2nitri',     default=ksO2nitri)
  call self%get_parameter(self%rODUox       ,'rODUox',        default=rODUox)
  call self%get_parameter(self%ksO2oduox    ,'ksO2oduox',     default=ksO2oduox)
  call self%get_parameter(self%ksO2oxic     ,'ksO2oxic',      default=ksO2oxic)
  call self%get_parameter(self%ksNO3denit   ,'ksNO3denit',    default=ksNO3denit)
  call self%get_parameter(self%kinO2denit   ,'kinO2denit',    default=kinO2denit)
  call self%get_parameter(self%kinNO3anox   ,'kinNO3anox',    default=kinNO3anox)
  call self%get_parameter(self%kinO2anox    ,'kinO2anox',     default=kinO2anox)

!!------- model parameters from nml-list omexdia_p_mpb ------- 
  call self%get_parameter(self%mumax        ,'mumax',         default=mumax)
  call self%get_parameter(self%alpha        ,'alpha',         default=alpha)
  call self%get_parameter(self%gamma        ,'gamma',         default=gamma)
  call self%get_parameter(self%Qmax         ,'Qmax',          default=Qmax)
  call self%get_parameter(self%thetamax     ,'thetamax',      default=thetamax)
  call self%get_parameter(self%KNH4         ,'KNH4',          default=KNH4)
  call self%get_parameter(self%resp         ,'resp',          default=resp)
if (MPhytoBenOn) then
      call self%get_parameter(self%Qmin         ,'Qmin',          default=Qmin)
      call self%get_parameter(self%uptmax       ,'uptmax',        default=uptmax)
      call self%get_parameter(self%KNO3         ,'KNO3',          default=KNO3)
      call self%get_parameter(self%KinNH4       ,'KinNH4',        default=KinNH4)
      call self%get_parameter(self%keps         ,'keps',          default=keps)
      call self%get_parameter(self%Kresp        ,'Kresp',         default=Kresp)
      call self%get_parameter(self%graz         ,'graz',          default=graz)
      call self%get_parameter(self%kout         ,'kout',          default=kout)
      call self%get_parameter(self%kexu         ,'kexu',          default=kexu)
      call self%get_parameter(self%rzoo         ,'rzoo',          default=rzoo)
      call self%get_parameter(self%PAR0max      ,'PAR0max',       default=PAR0max)
      call self%get_parameter(self%k0           ,'k0',            default=k0)
      call self%get_parameter(self%Achla        ,'Achla',         default=Achla)
      call self%get_parameter(self%bTemp        ,'bTemp',         default=bTemp)
end if

!!------- Register state variables  ------- 
  call self%register_state_variable(self%id_fdet,  'fdet','mmolC/m**3','fast detritus C', &
   fdet_init, minimum=_ZERO_, no_river_dilution=.true. )
  call self%set_variable_property(self%id_fdet,'particulate',.true.)
  call self%register_state_variable(self%id_sdet,  'sdet','mmolC/m**3','slow detritus C', &
   sdet_init, minimum=_ZERO_, no_river_dilution=.true. )
  call self%set_variable_property(self%id_sdet,'particulate',.true.)
  call self%register_state_variable(self%id_no3,   'no3','mmolN/m**3','dissolved nitrate', &
   no3_init, minimum=_ZERO_, no_river_dilution=.true. )
  call self%set_variable_property(self%id_no3,'particulate',.false.)
  call self%register_state_variable(self%id_nh3,   'nh3','mmolN/m**3','dissolved ammonium', &
   nh3_init, minimum=_ZERO_, no_river_dilution=.true. )
  call self%set_variable_property(self%id_nh3,'particulate',.false.)
  call self%register_state_variable(self%id_oxy,   'oxy','mmolO2/m**3','dissolved oxygen', &
   oxy_init, minimum=_ZERO_, no_river_dilution=.true. )
  call self%set_variable_property(self%id_oxy,'particulate',.false.)
  call self%register_state_variable(self%id_odu,   'odu','mmolO2/m**3','dissolved reduced substances', &
   odu_init, minimum=_ZERO_, no_river_dilution=.true. )
  call self%set_variable_property(self%id_odu,'particulate',.false.)

if (PhosphorusOn) then
      call self%register_state_variable(self%id_pdet,  'pdet','mmolP/m**3','detritus-P', &
       pdet_init, minimum=_ZERO_, no_river_dilution=.true. )
  call self%set_variable_property(self%id_pdet,'particulate',.true.)
      call self%register_state_variable(self%id_po4,   'po4','mmolP/m**3','dissolved phosphate', &
       po4_init, minimum=_ZERO_, no_river_dilution=.true. )
  call self%set_variable_property(self%id_po4,'particulate',.false.)
end if

if (MPhytoBenOn) then
      call self%register_state_variable(self%id_mpbCHL, 'mpbCHL','mmolN/m**3','MicroPhytoBenthos chlorophyll mpbCHL', &
       mpbCHL_init, minimum=_ZERO_, no_river_dilution=.true. )
  call self%set_variable_property(self%id_mpbCHL,'particulate',.true.)
      call self%register_state_variable(self%id_mpbC,  'mpbC','mmolC/m**3','MicroPhytoBenthos carbon mpbC', &
       mpbC_init, minimum=_ZERO_, no_river_dilution=.true. )
  call self%set_variable_property(self%id_mpbC,'particulate',.true.)
      call self%register_state_variable(self%id_mpbN,  'mpbN','mmolN/m**3','MicroPhytoBenthos nitrogen mpbN', &
       mpbN_init, minimum=_ZERO_, no_river_dilution=.true. )
  call self%set_variable_property(self%id_mpbN,'particulate',.true.)
      call self%register_state_variable(self%id_eps,   'eps','mmolC/m**3','Extracellular Polymeric Substances  eps', &
       eps_init, minimum=_ZERO_, no_river_dilution=.true. )
  call self%set_variable_property(self%id_eps,'particulate',.false.)
end if

!!------- Register diagnostic variables  ------- 
  call self%register_diagnostic_variable(self%id_adsP,    'adsP','mmolP/m**3', 'phosphate adsorption adsP', &
      output=output_instantaneous)
  call self%register_diagnostic_variable(self%id_denit,   'denit','mmolN/m**3/d', 'denitrification rate denit', &
      output=output_instantaneous)
  call self%register_diagnostic_variable(self%id_PrimProd, 'PrimProd','mmolC/m**3/d', 'MPB primary production rate PrimProd', &
      output=output_instantaneous)
  call self%register_diagnostic_variable(self%id_par,     'par','w/m2', 'photosynthetically active radiation par', &
      output=output_instantaneous)
  call self%register_diagnostic_variable(self%id_Q_N,     'Q_N','-', 'MPB nitrogen quota Q_N', &
      output=output_instantaneous)
  call self%register_diagnostic_variable(self%id_Q_chl,   'Q_chl','-', 'MPB CHL:C ratio Q_chl', &
      output=output_instantaneous)

!!------- Register environmental dependencies  ------- 
  call self%register_dependency(self%id_temp,varname_temp)
  call self%register_dependency(self%id_parz,varname_par)
!  call self%register_dependency(self%id_porosity,varname_porosity)

  return

!!-------  if files are not found ...  
90 call self%fatal_error('omexdia_p_init','Error reading namelist omexdia_p_switch.')
91 call self%fatal_error('omexdia_p_init','Error reading namelist omexdia_p_init.')
92 call self%fatal_error('omexdia_p_init','Error reading namelist omexdia_p_par.')
93 call self%fatal_error('omexdia_p_init','Error reading namelist omexdia_p_mpb.')
99 call self%fatal_error('omexdia_p_init','Namelist omexdia_p_switch was not found in file.')
100 call self%fatal_error('omexdia_p_init','Namelist omexdia_p_init was not found in file.')
101 call self%fatal_error('omexdia_p_init','Namelist omexdia_p_par was not found in file.')
102 call self%fatal_error('omexdia_p_init','Namelist omexdia_p_mpb was not found in file.')

end subroutine initialize

!!----------------------------------------------------------------------
!!   end of section generated by parser 
!!----------------------------------------------------------------------
! #SP#"
!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: Right hand sides of OMEXDIA+P model
!
! !INTERFACE:
   subroutine do(self,_ARGUMENTS_DO_)
!
! !DESCRIPTION:
!
! !INPUT PARAMETERS:
   class (type_hzg_omexdia_p),intent(in) :: self
   _DECLARE_ARGUMENTS_DO_
!
! !REVISION HISTORY:
!  Original author(s): Richard Hofmeister & Kai Wirtz
!
! !LOCAL VARIABLES:
   type (type_rhs)       :: rhsv
   real(rk) :: fdet,sdet,oxy,odu,no3,nh3,pdet,po4,mpbC, mpbN,mpbCHL,eps
   real(rk) :: temp,depth,temp_kelvin,f_T,E_a,sedDepth, par,parz,porosity
   real(rk) :: radsP,Oxicminlim,Denitrilim,Anoxiclim,Rescale,rP
   real(rk),parameter :: relaxO2=0.04_rk
   real(rk),parameter :: T0 = 288.15_rk ! reference Temperature fixed to 15 degC
   real(rk),parameter :: Q10b = 1.5_rk
   real(rk) :: CprodF,CprodS,CprodEPS,Cprod,Nprod,Pprod
   real(rk) :: AnoxicMin,Denitrific,OxicMin,Nitri,OduDepo,OduOx,pDepo
   real(rk) :: prodChl, k, theta, Q_N, Q_chl, Pmax,  PP, prod, prodeps, fac
   real(rk) :: prodO2, rhochl, uptNH4, uptNO3, uptchl, uptN, respphyto,faeces
   real(rk) :: exud, grazingC, grazingN, grazingChl, respzoo, exportC, exportN
!EOP
!-----------------------------------------------------------------------
!BOC
   ! Enter spatial loops (if any)
   _LOOP_BEGIN_
!#S_GET
!---------- GET for each state variable ----------
  _GET_(self%id_fdet, fdet)  ! fast detritus C in mmolC/m**3
  _GET_(self%id_sdet, sdet)  ! slow detritus C in mmolC/m**3
  _GET_(self%id_no3, no3)  ! dissolved nitrate in mmolN/m**3
  _GET_(self%id_nh3, nh3)  ! dissolved ammonium in mmolN/m**3
  _GET_(self%id_oxy, oxy)  ! dissolved oxygen in mmolO2/m**3
  _GET_(self%id_odu, odu)  ! dissolved reduced substances in mmolO2/m**3
if (self%PhosphorusOn) then
      _GET_(self%id_pdet, pdet)  ! detritus-P in mmolP/m**3
      _GET_(self%id_po4, po4)  ! dissolved phosphate in mmolP/m**3
end if
if (self%MPhytoBenOn) then
      _GET_(self%id_mpbCHL, mpbCHL)  ! MicroPhytoBenthos chlorophyll in mmolN/m**3
      _GET_(self%id_mpbC, mpbC)  ! MicroPhytoBenthos carbon in mmolC/m**3
      _GET_(self%id_mpbN, mpbN)  ! MicroPhytoBenthos nitrogen in mmolN/m**3
      _GET_(self%id_eps, eps)  ! Extracellular Polymeric Substances  in mmolC/m**3
end if
!#E_GET
!#S_GED
  _GET_(self%id_temp, temp)  ! sediment-water temperature
  _GET_(self%id_parz, parz)  ! sediment light normalized to one at top
!  _GET_(self%id_porosity, porosity)  ! sediment volumetric porosity
!#E_GED
   temp_kelvin = 273.15_rk + temp
   E_a=0.1_rk*log(Q10b)*T0*(T0+10.0_rk);
   f_T = 1.0_rk*exp(-E_a*(1.0_rk/temp_kelvin - 1.0_rk/T0))

   Oxicminlim = oxy/(oxy+self%ksO2oxic+relaxO2*(nh3+odu))                ! limitation terms
   Denitrilim = (1.0_rk-oxy/(oxy+self%kinO2denit)) * NO3/(no3+self%ksNO3denit)
   Anoxiclim  = (1.0_rk-oxy/(oxy+self%kinO2anox)) * (1.0_rk-no3/(no3+self%kinNO3anox))
   Rescale    = 1.0_rk/(Oxicminlim+Denitrilim+Anoxiclim)

   CprodF = self%rFast * fdet
   CprodS = self%rSlow * sdet 
   if (self%MPhytoBenOn) then
     CprodEPS = sqrt(self%rFast*self%rSlow) * eps
   else
     CprodEPS = 0.0_rk
   endif

   Cprod  = CprodF + CprodS + CprodEPS
   Nprod  = CprodF * self%NCrFdet + CprodS * self%NCrSdet

if (self%PhosphorusOn) then
! PO4-adsorption ceases when critical capacity is reached
! [FeS] approximated by ODU
   radsP  = self%PAds * self%rSlow * (po4*max(odu,self%PAdsODU))
   rP    = self%rFast * (1.0_rk - Oxicminlim)
   Pprod  = rP * pdet
endif

! Oxic mineralisation, denitrification, anoxic mineralisation
! then the mineralisation rates
   OxicMin    = Cprod*Oxicminlim*Rescale        ! oxic mineralisation
   Denitrific = Cprod*Denitrilim*Rescale        ! Denitrification
   AnoxicMin  = Cprod*Anoxiclim *Rescale        ! anoxic mineralisation

! reoxidation and ODU deposition
   Nitri      = f_T * self%rnit   * nh3 * oxy/(oxy + self%ksO2nitri + relaxO2*(fdet + odu))
   OduOx      = f_T * self%rODUox * odu * oxy/(oxy + self%ksO2oduox + relaxO2*(nh3 + fdet))

!  pDepo      = min(1.0_rk,0.233_rk*(wDepo)**0.336_rk )
   pDepo      = 0.0_rk
   OduDepo    = AnoxicMin*pDepo 
#define _CONV_UNIT_ /secs_pr_day

!#S__RHS
!#E__RHS
!---------- RHS for MicroPhytoBenthos----------
!----- Light field
!max(0, PAR0max * cos()
if (self%MPhytoBenOn) then
  par    = self%PAR0max * parz
!  k      = self%k0 + self%Achla * mpbCHL

!----- intracellular N:C:Chl stoichiometry
  Q_N    = mpbN   / mpbC
  Q_chl  = mpbCHL / mpbC 
  theta  = mpbCHL / mpbN

!----- photosnythesis rate 
  fac    = 1.0 - self%Qmin/Q_N
  if (fac .lt. 0.0) fac = 0.0
  Pmax   = 2*self%mumax * exp(self%bTemp*temp) * fac
  PP     = Pmax * (1.0 - exp(-self%alpha * parz*Q_chl/Pmax))
  prod   = PP * mpbC
  prodO2 = self%gamma * prod

  rhochl = self%thetamax * PP/(self%alpha * parz * Q_chl)

!----- nutrient uptake (TODO add dependencies on P, Si)
  uptNO3 = self%uptmax * no3/(no3 + self%KNO3)
  uptNH4 = self%uptmax * nh3/(nh3 + self%KNH4)
  uptN   = uptNO3 + uptNH4
!----- chlorophyll synthesis
  fac    = 1.0 - theta/self%thetamax
  if (fac .lt. 0.0) fac = 0.0
  uptchl = uptN * fac/(fac + 0.05)
  prodChl= rhochl * uptchl * mpbC

  fac    = (self%Qmax - Q_N) / (self%Qmax - self%Qmin)
  if (fac .lt. 0.0) fac = 0.0
  uptNO3 = uptNO3 * fac * (1.0 - nh3/(nh3 + self%KinNH4)) * mpbC
  uptNH4 = uptNH4 * fac * mpbC
  uptN   = uptNO3 + uptNH4

! Carbohydrate exudation:
  prodeps = self%keps * prod 

! Respiration:
  respphyto = self%resp * mpbC * oxy/(oxy+self%Kresp)

! Zoobenthos grazing and associated processes:
  grazingC = self%graz * mpbC
  grazingN = self%graz * mpbN
  grazingChl = self%graz * mpbCHL
  faeces   = self%kout * grazingC
  exud     = self%kexu * grazingN
  respzoo  = self%rzoo * grazingC * oxy/(oxy+self%Kresp)
  exportC  = (1.0 - self%kout) * grazingC - respzoo
  exportN  = (1.0 - self%kexu) * grazingN - faeces* self%NCrFdet

!  dynamics of mpbCHL ~ MicroPhytoBenthos chlorophyll
    rhsv%mpbCHL = ( prodchl - grazingChl ) _CONV_UNIT_
!  dynamics of mpbC ~ MicroPhytoBenthos carbon
    rhsv%mpbC = ( prod - grazingC - respphyto - prodeps ) _CONV_UNIT_
!  dynamics of mpbN ~ MicroPhytoBenthos nitrogen
    rhsv%mpbN = ( uptN - grazingN ) _CONV_UNIT_
!  dynamics of eps ~ Extracellular Polymeric Substances 
    rhsv%eps = ( prodeps - f_T * CprodEPS ) _CONV_UNIT_
else
  respzoo   = 0.0_rk
  respphyto = 0.0_rk
  faeces    = 0.0_rk
  exud      = 0.0_rk
  uptNH4    = 0.0_rk
  uptNO3    = 0.0_rk
end if

!---------- RHS for BGC/diagensis model part ----------
!  dynamics of fdet ~ fast detritus C
  rhsv%fdet = (-f_T * CprodF + faeces) _CONV_UNIT_
!  dynamics of sdet ~ slow detritus C
  rhsv%sdet = -f_T * CprodS _CONV_UNIT_
!  dynamics of oxy ~ dissolved oxygen
  rhsv%oxy = (-OxicMin - 2.0_rk* Nitri - OduOx - respzoo - respphyto) _CONV_UNIT_  !RH 1.0->150/106*OxicMin (if [oxy]=mmolO2/m**3)
!  dynamics of odu ~ dissolved reduced substances
  rhsv%odu = (AnoxicMin - OduOx - OduDepo) _CONV_UNIT_
!  dynamics of no3 ~ dissolved nitrate
  rhsv%no3 = (-0.8_rk*Denitrific + Nitri - uptNO3) _CONV_UNIT_
!  dynamics of nh3 ~ dissolved ammonium
  rhsv%nh3 = (f_T * Nprod - Nitri + exud - uptNH4 ) / (1.0_rk + self%NH3Ads) _CONV_UNIT_
if (self%PhosphorusOn) then
!  dynamics of pdet ~ detritus-P
  rhsv%pdet = (radsP - f_T * Pprod) _CONV_UNIT_
!  dynamics of po4 ~ dissolved phosphate
  rhsv%po4 = (f_T * Pprod - radsP) _CONV_UNIT_
end if

!#S_ODE
!---------- ODE for each state variable ----------
  _SET_ODE_(self%id_fdet, rhsv%fdet)
  _SET_ODE_(self%id_sdet, rhsv%sdet)
  _SET_ODE_(self%id_no3, rhsv%no3)
  _SET_ODE_(self%id_nh3, rhsv%nh3)
  _SET_ODE_(self%id_oxy, rhsv%oxy)
  _SET_ODE_(self%id_odu, rhsv%odu)
if (self%PhosphorusOn) then
      _SET_ODE_(self%id_pdet, rhsv%pdet)
      _SET_ODE_(self%id_po4, rhsv%po4)
end if
if (self%MPhytoBenOn) then
      _SET_ODE_(self%id_mpbCHL, rhsv%mpbCHL)
      _SET_ODE_(self%id_mpbC, rhsv%mpbC)
      _SET_ODE_(self%id_mpbN, rhsv%mpbN)
      _SET_ODE_(self%id_eps, rhsv%eps)
end if
!#E_ODE

!#S_DIA
  _SET_DIAGNOSTIC_(self%id_adsP, radsP)                     !step_integrated phosphate adsorption
  _SET_DIAGNOSTIC_(self%id_denit, Denitrific)               !last denitrification rate
  _SET_DIAGNOSTIC_(self%id_PrimProd, PP)                    !step_integrated MPB primary production rate
  _SET_DIAGNOSTIC_(self%id_par, rhsv%mpbC)                        !step_integrated photosynthetically active radiation
  _SET_DIAGNOSTIC_(self%id_Q_N, Q_N)                        !step_integrated MPB nitrogen quota
  _SET_DIAGNOSTIC_(self%id_Q_chl, Q_chl)                    !step_integrated MPB CHL:C ratio
!#E_DIA
   ! Export diagnostic variables
!   _SET_DIAGNOSTIC_(self%id_denit,Denitrific)
!   _SET_DIAGNOSTIC_(self%id_adsp ,radsP)

   ! Leave spatial loops (if any)
   _LOOP_END_

   end subroutine do
!EOC

!-----------------------------------------------------------------------
!BOP
!
! !IROUTINE: Get the total of conserved quantities (currently only nitrogen)
!
! !INTERFACE:
   subroutine get_conserved_quantities(self,_ARGUMENTS_GET_CONSERVED_QUANTITIES_)
! !INPUT PARAMETERS:
   class (type_hzg_omexdia_p), intent(in) :: self
   _DECLARE_ARGUMENTS_GET_CONSERVED_QUANTITIES_
! !REVISION HISTORY:
!  Original author(s): Jorn Bruggeman
!
! !LOCAL VARIABLES:
 real(rk) ::  fdet, sdet, pdet, po4, no3, nh3, mpbC, mpbN, eps
!
!EOP
!-----------------------------------------------------------------------
!BOC
   ! Enter spatial loops (if any)
   _LOOP_BEGIN_

   ! Retrieve current (local) state variable values.
!#S__CON
!#E__CON
 _GET_(self%id_fdet, fdet)  ! fast detritus C in mmolC/m**3
 _GET_(self%id_sdet, sdet)  ! slow detritus C in mmolC/m**3
 _GET_(self%id_pdet, pdet)  ! detritus-P in mmolP/m**3
 _GET_(self%id_po4, po4)  ! dissolved phosphate in mmolP/m**3
 _GET_(self%id_no3, no3)  ! dissolved nitrate in mmolN/m**3
 _GET_(self%id_nh3, nh3)  ! dissolved ammonium in mmolN/m**3
 _GET_(self%id_mpbC, mpbC)  ! MicroPhytoBenthos carbon in mmolC/m**3
 _GET_(self%id_mpbN, mpbN)  ! MicroPhytoBenthos nitrogen in mmolN/m**3
 _GET_(self%id_eps, eps)  ! Extracellular Polymeric Substances  in mmolC/m**3

! todo include porosity, RH: all states are defined relative to pore water volume -> no porosity needed 
! todo: change to aggregate variables:
! _SET_CONSERVED_QUANTITY_(self%id_totC, fdet + sdet+ mpbC + eps)
! _SET_CONSERVED_QUANTITY_(self%id_totN, nh3  + no3 + mpbN)
! _SET_CONSERVED_QUANTITY_(self%id_totP, pdet + po4 )

   ! Leave spatial loops (if any)
   _LOOP_END_
   end subroutine get_conserved_quantities
!EOC

   end module fabm_hzg_omexdia_p

